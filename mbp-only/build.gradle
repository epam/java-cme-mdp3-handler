plugins {
    id "me.champeau.gradle.jmh" version "0.5.0-rc-1"
    id 'distribution'
}

archivesBaseName = "$moduleNamePrefix-mbp"

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    implementation project(':core')
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: "$commonsLang3Version"
    implementation group: 'net.openhft', name: 'koloboke-api-jdk8', version: "$openhftKolobokeVersion"
    runtimeOnly group: 'net.openhft', name: 'koloboke-impl-jdk8', version: "$openhftKolobokeVersion"
    testImplementation group: 'uk.co.real-logic', name: 'sbe-tool', version: '1.6.0'
    testImplementation 'io.cucumber:cucumber-java:4.3.1'
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/cucumber/resources']
        }
    }
}

task prepareRtmLibs(type: Copy) {
    into "$buildDir/rtmlibs/libs"
    from configurations.runtimeClasspath
}
build.finalizedBy prepareRtmLibs

distributions {
    main {
        distributionBaseName = "$archivesBaseName"
        contents {
            from { '../rtmlibs' }
            from { '../dist' }
        }
    }
}
