plugins {
    id "me.champeau.gradle.jmh" version "0.5.0-rc-1"
    id 'distribution'
}

archivesBaseName = "$moduleNamePrefix-mbo"

dependencies {
    implementation project(':core')
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: "$commonsLang3Version"
    implementation group: 'org.agrona', name: 'agrona', version: "$agronaVersion"
    implementation group: 'net.openhft', name: 'koloboke-api-jdk8', version: "$openhftKolobokeVersion"
    runtimeOnly group: 'net.openhft', name: 'koloboke-impl-jdk8', version: "$openhftKolobokeVersion"
    testImplementation group: 'uk.co.real-logic', name: 'sbe-tool', version: '1.6.0'
    testImplementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

distributions {
    main {
        distributionBaseName = "$archivesBaseName"
        contents {
            from { '../dist' }
            from { "$buildDir/rtm-libs" }
        }
    }
}

task prepareRtmLibs(type: Copy) {
    into "$buildDir/rtm-libs"
    from configurations.runtimeClasspath
}
build.finalizedBy prepareRtmLibs

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
    manifest {
        attributes 'Main-Class': 'com.epam.cme.mdp3.test.MBOWithMBPMain'
    }
}
